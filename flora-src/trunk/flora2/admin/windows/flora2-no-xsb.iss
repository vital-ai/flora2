; Script generated by the Inno Setup Script Wizard.
; Modified from xsb.iss
; Double click on this file to run with inno, then build-menu, compile.
; A compiled version of Flora-2 must exist at the location pointed
; by the MyBaseDir variable below.

#define MyAppName "Flora-2"
#define FlrVersion "2.1"
#define MyAppVerName "Flora-2  v. "+FlrVersion+" (Punica granatum)"
#define MyAppPublisher "Flora-2"
#define MyAppURL "http://flora.sourceforge.net/"
#define MyAppUrlName "Flora-2 Web Site.url"
#define MyAppLicenseURL "http://www.apache.org/licenses/LICENSE-2.0"
#define MyAppLicenseUrlName "Apache License Web Site.url"

#define FLORA_DIR "{reg:HKLM\SYSTEM\CurrentControlSet\Control\Session Manager\Environment,FLORA_DIR|{pf}\flora2}"
#define MyBaseDir "H:\FLORA\flora2"

[Setup]
AppName={#MyAppName}
AppVerName={#MyAppVerName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
ChangesEnvironment=yes
DefaultDirName={#FLORA_DIR}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
LicenseFile={#MyBaseDir}\LICENSE
InfoBeforeFile={#MyBaseDir}\admin\windows\InstallMessage
OutputBaseFilename=flora2-{#FlrVersion}
Compression=lzma
SolidCompression=yes
PrivilegesRequired=none
TouchTime=none
InstallYear=2022

VersionInfoVersion={#FlrVersion}
VersionInfoCopyright=� The Research Foundation of SUNY, 1999 - {#InstallYear}; Coherent Knowledge Systems, 2013 - {#InstallYear}

AllowRootDirectory=yes
UninstallFilesDir="{userdocs}\Flora-2 uninstaller"

MinVersion=0,5.1

[Types]
Name: "full"; Description: "Full Flora-2 installation (recommended)"
Name: "base"; Description: "Base Flora-2 installation"

[Components]
Name: "base"; Description: "Base system"; Types: full base; Flags: disablenouninstallwarning
Name: "base\sources"; Description: "Base system plus the source files"; Types: full base; Flags: disablenouninstallwarning
Name: "documentation"; Description: "Documentation"; Types: full; Flags: disablenouninstallwarning

[Tasks]
Name: website; Description: "&Visit {#MyAppName} web site"; Components: base
Name: shortcut; Description: "&Create a desktop shortcut to run Flora-2"; Components: base

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Messages]
BeveledLabel=Flora-2 {#FlrVersion} � The Research Foundation of SUNY, 1999 - {#InstallYear}; Coherent Knowledge Systems, 2013 - {#InstallYear}

[Dirs]
Name: "{app}" ; Permissions: users-modify users-full
Name: "{userdocs}\Flora-2 uninstaller"

[Files]
Source: "{#MyBaseDir}\*.xwam"; Excludes: ".*,CVS"; DestDir: "{app}\"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\*.flh"; Excludes: ".*,CVS"; DestDir: "{app}\"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\floraconfig.bat"; Excludes: ".*,CVS"; DestDir: "{app}\"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\runflora.bat"; Excludes: ".*,CVS"; DestDir: "{app}\"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\LICENSE"; Excludes: ".*,CVS"; DestDir: "{app}\"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\etc\flora-desktop.ico"; Excludes: ".*,CVS"; DestDir: "{app}\etc"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\etc\flora.ico"; Excludes: ".*,CVS"; DestDir: "{app}\etc"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\*.P"; Excludes: ".*,CVS"; DestDir: "{app}\"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\*.H"; Excludes: ".*,CVS"; DestDir: "{app}\"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\.flora_buildinfo.P"; Excludes: ".*,CVS"; DestDir: "{#flrapp}\"; Components: base\sources; Flags: ignoreversion
Source: "{#MyBaseDir}\makeflora.bat"; Excludes: ".*,CVS"; DestDir: "{app}\"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\NMakefile.mak"; Excludes: ".*,CVS"; DestDir: "{app}\"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\AT\*.flr"; Excludes: ".*,CVS"; DestDir: "{app}\AT"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\AT\include\*.flh"; Excludes: ".*,CVS"; DestDir: "{app}\AT\include"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\AT\prolog\*.P"; Excludes: ".*,CVS"; DestDir: "{app}\AT\prolog"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\AT\prolog\*.xwam"; Excludes: ".*,CVS"; DestDir: "{app}\AT\prolog"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\AT\NMakefile.mak"; Excludes: ".*,CVS"; DestDir: "{app}\AT"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\closure\*.flh"; Excludes: ".*,CVS"; DestDir: "{app}\closure"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\closure\*.fli"; Excludes: ".*,CVS"; DestDir: "{app}\closure"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\closure\*.inc"; Excludes: ".*,CVS"; DestDir: "{app}\closure"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\closure\NMakefile.mak"; Excludes: ".*,CVS"; DestDir: "{app}\closure"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\datatypes\*.xwam"; Excludes: ".*,CVS"; DestDir: "{app}\datatypes"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\datatypes\*.P"; Excludes: ".*,CVS"; DestDir: "{app}\datatypes"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\datatypes\NMakefile.mak"; Excludes: ".*,CVS"; DestDir: "{app}\datatypes"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\debugger\*.xwam"; Excludes: ".*,CVS"; DestDir: "{app}\debugger"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\debugger\*.dat"; Excludes: ".*,CVS"; DestDir: "{app}\debugger"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\debugger\*.P"; Excludes: ".*,CVS"; DestDir: "{app}\debugger"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\debugger\*.in"; Excludes: ".*,CVS"; DestDir: "{app}\debugger"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\debugger\NMakefile.mak"; Excludes: ".*,CVS"; DestDir: "{app}\debugger"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\demos\*.flr"; Excludes: ".*,CVS"; DestDir: "{app}\demos"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\demos\sgml\*.flr"; Excludes: ".*,CVS"; DestDir: "{app}\demos\sgml"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\demos\xpath\*.flr"; Excludes: ".*,CVS"; DestDir: "{app}\demos\xpath"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\demos\sgml\expectedoutput"; Excludes: ".*,CVS"; DestDir: "{app}\demos\expectedoutput"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\demos\xpath\expectedoutput"; Excludes: ".*,CVS"; DestDir: "{app}\xpath\expectedoutput"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\demos\NMakefile.mak"; Excludes: ".*,CVS"; DestDir: "{app}\demos"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\docs\*.pdf"; Excludes: ".*,CVS"; DestDir: "{app}\docs"; Components: documentation; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\docs\NMakefile.mak"; Excludes: ".*,CVS"; DestDir: "{app}\docs"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\emacs\*.el"; Excludes: ".*,CVS"; DestDir: "{app}\emacs"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\emacs\NMakefile.mak"; Excludes: ".*,CVS"; DestDir: "{app}\emacs"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\emacs\README"; Excludes: ".*,CVS"; DestDir: "{app}\emacs"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\flrincludes\*.flh"; Excludes: ".*,CVS"; DestDir: "{app}\flrincludes"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\genincludes\*.flh"; Excludes: ".*,CVS"; DestDir: "{app}\genincludes"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\genincludes\*.fli"; Excludes: ".*,CVS"; DestDir: "{app}\genincludes"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\genincludes\*.inc"; Excludes: ".*,CVS"; DestDir: "{app}\genincludes"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\genincludes\NMakefile.mak"; Excludes: ".*,CVS"; DestDir: "{app}\genincludes"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\headerinc\*.flh"; Excludes: ".*,CVS"; DestDir: "{app}\headerinc"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\includes\*.flh"; Excludes: ".*,CVS"; DestDir: "{app}\includes"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\includes\*.fli"; Excludes: ".*,CVS"; DestDir: "{app}\includes"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\includes\NMakefile.mak"; Excludes: ".*,CVS"; DestDir: "{app}\includes"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\lib\*.flr"; Excludes: ".*,CVS"; DestDir: "{app}\lib"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\lib\NMakefile.mak"; Excludes: ".*,CVS"; DestDir: "{app}\lib"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\lib\include\*.flh"; Excludes: ".*,CVS"; DestDir: "{app}\lib"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\libinc\*.flh"; Excludes: ".*,CVS"; DestDir: "{app}\libinc"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs


Source: "{#MyBaseDir}\cc\*.P"; Excludes: ".*,CVS"; DestDir: "{app}\cc"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\cc\prolog2hilog.*"; Excludes: ".*,CVS"; DestDir: "{app}\cc"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\cc\flora_ground.*"; Excludes: ".*,CVS"; DestDir: "{app}\cc"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\cc\NMakefile.mak"; Excludes: ".*,CVS"; DestDir: "{app}\cc"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\cc\NMakefile64.mak"; Excludes: ".*,CVS"; DestDir: "{app}\cc"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\cc\windows\*.dll"; Excludes: ".*,CVS"; DestDir: "{app}\cc\windows"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\cc\windows\*.exp"; Excludes: ".*,CVS"; DestDir: "{app}\cc\windows"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\cc\windows\*.lib"; Excludes: ".*,CVS"; DestDir: "{app}\cc\windows"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\cc\windows64\*.dll"; Excludes: ".*,CVS"; DestDir: "{app}\cc\windows64"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\cc\windows64\*.exp"; Excludes: ".*,CVS"; DestDir: "{app}\cc\windows64"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\cc\windows64\*.lib"; Excludes: ".*,CVS"; DestDir: "{app}\cc\windows64"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\pkgs\*.flr"; Excludes: ".*,CVS"; DestDir: "{app}\pkgs"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\pkgs\include\*.flh"; Excludes: ".*,CVS"; DestDir: "{app}\pkgs\include"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\pkgs\prolog\*.P"; Excludes: ".*,CVS"; DestDir: "{app}\pkgs\prolog"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\pkgs\NMakefile.mak"; Excludes: ".*,CVS"; DestDir: "{app}\pkgs"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\syslib\*.xwam"; Excludes: ".*,CVS"; DestDir: "{app}\syslib"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\syslib\*.P"; Excludes: ".*,CVS"; DestDir: "{app}\syslib"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\syslib\*.H"; Excludes: ".*,CVS"; DestDir: "{app}\syslib"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MyBaseDir}\syslib\NMakefile.mak"; Excludes: ".*,CVS"; DestDir: "{app}\syslib"; Components: base\sources; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\syslibinc\*.flh"; Excludes: ".*,CVS"; DestDir: "{app}\syslibinc"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#MyBaseDir}\java\*"; Excludes: ".*,CVS"; DestDir: "{app}\java"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

[INI]
Filename: "{app}\{#MyAppUrlName}"; Section: "InternetShortcut"; Key: "URL"; String: "{#MyAppURL}"; Components: base
Filename: "{app}\{#MyAppLicenseUrlName}"; Section: "InternetShortcut"; Key: "URL"; String: "{#MyAppLicenseURL}"; Components: base


[Icons]
Name: "{group}\Flora-2"; Filename: "cmd" ; Parameters: "/k ""cd {app} & runflora.bat"""; Comment: "Runs Flora-2 within a Windows command window"; WorkingDir: "{app}"; Components: base; IconFilename: "{app}\etc\flora.ico"

Name: "{userdesktop}\Flora-2"; Filename: "cmd" ; Parameters: "/k ""cd {app} & runflora.bat"""; Comment: "Runs Flora-2 within a Windows command window"; WorkingDir: "{app}"; Components: base; IconFilename: "{app}\etc\flora-desktop.ico"; Tasks: shortcut

Name: "{group}\License"; Filename: "{#MyAppLicenseURL}"; Components: base

Name: "{group}\Web Site"; Filename: "{#MyAppURL}"; Components: base

Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"; Components: base

[Registry]
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "FLORA_DIR"; ValueData: "{app}"; Components: base; Flags: deletevalue uninsdeletevalue

[Run]
Filename: "cmd" ; Parameters: "/c """"{app}\floraconfig.bat"" ""{code:GetXSBLocation|{app}}"""""; WorkingDir: "{app}"
Filename: "{app}\{#MyAppUrlName}"; Flags: shellexec nowait; Tasks: website

[UninstallDelete]
Type: filesandordirs; Name: "{app}"; Components: base
Type: filesandordirs; Name: "{group}"; Components: base

[Code]
var Page: TInputFileWizardPage;

procedure InitializeWizard();
begin
  Page := CreateInputFilePage(wpSelectDir,
    'Specify the location of the XSB invocation script.',
    'It should look like something\XSB\bin\xsb64.bat (or ...\xsb.bat if you are using a 32-bit version of XSB).',
    'Pls use the Browse button to navigate to the desired script.');
  Page.Add('Location of xsb.bat or xsb64.bat: ', '*.bat', '*.bat');
end;

function GetXSBLocation(Param: String): String;
begin
  // Read value into variable
  Result := Page.Values[0];
end;

